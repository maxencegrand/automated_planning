(define (domain zenotravel)
	(:requirements :typing :hierarchy :method-preconditions :negative-preconditions)
	(:types
		Time - Object
		City - Object
		Aircraft - Thing
		Person - Thing
		Thing - Object
		FLevel - Object
	)
	(:predicates
		(at ?arg0 - Thing ?arg1 - City)
		(in ?arg0 - Person ?arg1 - Aircraft)
		(next ?arg0 - FLevel ?arg1 - FLevel)
		(max ?arg0 - FLevel)
        (min ?arg0 - FLevel)
        (not-min ?arg0 - FLevel)
		(fuel-level ?arg0 - Aircraft ?arg1 - FLevel)
	)


    (:task transport-person
		:parameters (?p - Person ?c - City)
	)

	(:task transport-aircraft
		:parameters (?a - Aircraft ?c - City)
	)

	(:method m0-ordering-0
		:parameters (?c - City ?p - Person)
		:task (transport-person ?p ?c)
		:precondition (and
			(at ?p ?c)
		)
		:subtasks (
		)
	)

	(:method m1-ordering-0
		:parameters (?a - Aircraft ?c1 - City ?c2 - City ?p - Person)
		:task (transport-person ?p ?c2)
		:precondition (and
			(at ?p ?c1)
    		(not(at ?p ?c2))
		)
		:ordered-subtasks (and
		 (transport-aircraft ?a ?c1)
		 (board ?p ?a ?c1)
		 (transport-aircraft ?a ?c2)
		 (debark ?p ?a ?c2)
		)
	)

	(:method m2-abort-ordering-0
		:parameters (?a - Aircraft ?c - City)
		:task (transport-aircraft ?a ?c)
		:precondition (and
			(at ?a ?c)
		)
		:ordered-subtasks (
		)
	)

	(:method m3-do-ordering-0
		:parameters (?a - Aircraft ?c - City ?other - City ?l1 - FLevel ?l2 - FLevel)
		:task (transport-aircraft ?a ?c)
		:precondition (and
			(not (at ?a ?c))
			(at ?a ?other)
            (fuel-level ?a ?l1)
			(next ?l2 ?l1)
            (not-min ?l1)
		)
		:ordered-subtasks (and
		 (fly ?a ?other ?c ?l1 ?l2)
		)
	)

    (:method m4-do-ordering-0
		:parameters (?a - Aircraft ?c - City ?other - City ?l1 - FLevel ?lmax - FLevel)
		:task (transport-aircraft ?a ?c)
		:precondition (and
			(not (at ?a ?c))
			(at ?a ?other)
            (fuel-level ?a ?l1)
			(min ?l1)
            (max ?lmax)
		)
		:ordered-subtasks (and
         (refuel ?a ?l1 ?lmax)
		 (transport-aircraft ?a ?c)
		)
	)

	(:action board
		:parameters (?p - Person ?a - Aircraft ?c - City)
		:precondition
			(and
				(at ?p ?c)
                (at ?a ?c)
			)
		:effect
			(and
				(not (at ?p ?c))
				(in ?p ?a)
			)
	)

	(:action debark
		:parameters (?p - Person ?a - Aircraft ?c - City)
		:precondition
			(and
				(in ?p ?a)
                (at ?a ?c)
			)
		:effect
			(and
				(not (in ?p ?a))
				(at ?p ?c)
			)
	)

	(:action refuel
		:parameters (?a - Aircraft ?l - FLevel ?l1 - FLevel)
		:precondition
			(and
				(fuel-level ?a ?l)
			)
		:effect
			(and
				(not (fuel-level ?a ?l))
				(fuel-level ?a ?l1)
			)
	)

	(:action fly
		:parameters (?a - Aircraft ?c1 - City ?c2 - City ?l1 - FLevel ?l2 - FLevel)
		:precondition
			(and
				(at ?a ?c1)
				(fuel-level ?a ?l1)
                (next ?l2 ?l1)
			)
		:effect
			(and
				(not (at ?a ?c1))
				(at ?a ?c2)
				(not (fuel-level ?a ?l1))
				(fuel-level ?a ?l2)
			)
	)
)
